buildscript {
    repositories {
        maven { url = 'https://maven.parchmentmc.org' }
    }
}

plugins {
    id 'fabric-loom' version '1.+'
    id 'maven-publish'
    id 'idea'
}

archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"

    mappings loom.layered() {
        officialMojangMappings()
//        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip") Waiting for 1.19.4
        parchment("org.parchmentmc.data:parchment-1.19.3:${parchment_version}@zip")
    }

    // Waiting on Fabric version, for now compiling against forge so code builds
    modCompileOnly "curse.maven:moonlight-499980:${moonlight_forge_id}"

    modRuntimeOnly "dev.lambdaurora:spruceui:${spruceui_version}"
    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"
    modRuntimeOnly "maven.modrinth:huge-structure-blocks:${hugestructureblocks_version}"
    modRuntimeOnly "curse.maven:worldedit-225608:${worldedit_id}"
    modRuntimeOnly "maven.modrinth:midnightlib:${midnightlib_version}-fabric"
    modRuntimeOnly "com.terraformersmc:modmenu:${modmenu_version}"

    implementation project(":Common")
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    modImplementation "maven.modrinth:lambdynamiclights:${lambdynamiclights_version}"
    modImplementation "curse.maven:moonlight-499980:${moonlight_fabric_id}"
    modImplementation "software.bernie.geckolib:geckolib-fabric-${minecraft_version}:${geckolib_version}"
    modImplementation("net.tslat.smartbrainlib:SmartBrainLib-fabric-${minecraft_version}:${smartbrainlib_version}")
    modImplementation "maven.modrinth:jade:${jade_version}"
//    modImplementation "vazkii.patchouli:Patchouli:${patchouli_version}-FABRIC"
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-api:${cca_version}")
    modImplementation("curse.maven:configured-667378:${configured_fabric_id}")
    modImplementation "net.tslat.tes:TES-fabric-${minecraft_version}:${tes_version}"
//    modImplementation("dev.dhyces.trimmed:trimmed-fabric-${minecraft_version}:${trimmed_version}")
    modImplementation("dev.dhyces.trimmed:trimmed-fabric-${previous_minecraft_version}:${trimmed_version}") // TODO: update

    include modImplementation("dev.kosmx.player-anim:player-animation-lib-fabric:${playeranimator_version}")
    include modImplementation("fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${forgeconfigapiport_version}")
    include modImplementation("com.jamieswhiteshirt:reach-entity-attributes:$reachentityattributes_version")
    include modImplementation("dev.thomasglasser.sherdsapi:sherdsapi-fabric-${minecraft_version}:${sherdsapi_version}")

    include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}")
    include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}")
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }

    mixin {
        add sourceSets.main, "minejago.refmap.json"
    }

    accessWidenerPath = project(":Common").file("src/main/resources/minejago.accesswidener")
}


processResources {
    from project(":Common").sourceSets.main.resources
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
