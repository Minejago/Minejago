buildscript {
    repositories {
        maven { url = 'https://maven.parchmentmc.org' }
    }
}

plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
}

archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"

    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
    }

    // Waiting on Fabric version, for now compiling against forge so code builds
    modCompileOnly "curse.maven:moonlight-499980:${moonlight_forge_id}"

    modRuntimeOnly "dev.lambdaurora:spruceui:${spruceui_version}+${minecraft_version}"
    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"
    modRuntimeOnly "maven.modrinth:huge-structure-blocks:${hugestructureblocks_version}"
    modRuntimeOnly "curse.maven:worldedit-225608:${worldedit_id}"
    modRuntimeOnly "com.terraformersmc:modmenu:${modmenu_version}"

    implementation project(":Common")
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}+${minecraft_version}"
    modImplementation "com.github.glitchfiend:TerraBlender-fabric:${minecraft_version}-${terrablender_version}"
    modImplementation "maven.modrinth:lambdynamiclights:${lambdynamiclights_version}+${minecraft_version}"
//    modImplementation "curse.maven:moonlight-499980:${moonlight_fabric_id}" Waiting on Fabric update
    modCompileOnly "curse.maven:moonlight-499980:${moonlight_forge_id}"
    modImplementation "software.bernie.geckolib:geckolib-fabric-${minecraft_version}:${geckolib_version}"
    modImplementation "curse.maven:smartbrainlib-661293:${smartbrainlib_fabric_id}-sources-${smartbrainlib_sources_fabric_id}"
    modImplementation "maven.modrinth:jade:${jade_fabric_version}"

    include modImplementation("dev.kosmx.player-anim:player-animation-lib-fabric:${playeranimator_version}+${minecraft_version}")
    include modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-api:${cca_version}")
    include modImplementation("fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${forgeconfigapiport_version}")
//    include modImplementation("com.jamieswhiteshirt:reach-entity-attributes:$reachentityattributes_version") Waiting on 1.19.3 update
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            vmArg "-XX:+AllowEnhancedClassRedefinition"
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            vmArg "-XX:+AllowEnhancedClassRedefinition"
            runDir("run")
        }
    }

    mixin {
        add sourceSets.main, "minejago.refmap.json"
    }

    accessWidenerPath = project(":Common").file("src/main/resources/minejago.accesswidener")
}


processResources {
    from project(":Common").sourceSets.main.resources
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
