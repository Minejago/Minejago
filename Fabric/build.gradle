plugins {
    id 'fabric-loom' version '1.+'
    id 'maven-publish'
    id 'idea'
}

archivesBaseName = "${mod_id}-fabric-${minecraft_version}"

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"

    mappings loom.layered() {
        officialMojangMappings()
//        parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
        parchment("org.parchmentmc.data:parchment-1.20.2:${parchment_version}@zip")
    }

    // Required for the rei forge annotation
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-forge:$rei_version"

    implementation project(":Common")
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    // Common
    include modImplementation("dev.kosmx.player-anim:player-animation-lib-fabric:${playeranimator_version}")
    include modImplementation("fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${forgeconfigapiport_version}")
    include modImplementation("com.jamieswhiteshirt:reach-entity-attributes:$reachentityattributes_version")
    include modImplementation("maven.modrinth:sherdsapi:${sherdsapi_version}-fabric")
    include modImplementation("maven.modrinth:trimmed:${trimmed_version}+fabric")

    modImplementation "net.tslat.smartbrainlib:SmartBrainLib-fabric-${minecraft_version}:${sbl_version}"
    modImplementation "software.bernie.geckolib:geckolib-fabric-${minecraft_version}:${geckolib_version}"
    modImplementation "maven.modrinth:jade:${jade_version}"
    modImplementation "me.shedaniel:RoughlyEnoughItems-fabric:$rei_version"
    modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }
//    modImplementation "com.klikli_dev:modonomicon-${minecraft_version}-fabric:${modonomicon_version}"
    modImplementation "com.klikli_dev:modonomicon-1.20.2-fabric:${modonomicon_version}"
    modImplementation "net.tslat.tes:TES-fabric-${minecraft_version}:${tes_version}"
    modImplementation "maven.modrinth:moonlight:fabric_${moonlight_version}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-api:${cca_version}"
    modImplementation "maven.modrinth:lambdynamiclights:${lambdynamiclights_version}"
    modImplementation "mysticdrew:common-networking-fabric:${common_networking_version}"

    // Fabric Only
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    // Runtime Only Deps
    modRuntimeOnly "dev.lambdaurora:spruceui:${spruceui_version}"

    // Runtime
    runtimeOnly "com.github.llamalad7.mixinextras:mixinextras-fabric:${mixinextras_version}"

    modRuntimeOnly "maven.modrinth:huge-structure-blocks:${hugestructureblocks_version}"
//    modRuntimeOnly "maven.modrinth:worldedit:${worldedit_version}"
    modRuntimeOnly "maven.modrinth:worldedit:${worldedit_id}"
    modRuntimeOnly "com.terraformersmc:modmenu:${modmenu_version}"

    include "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}"
    include "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}"
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }

    mixin {
        add sourceSets.main, "${mod_id}.refmap.json"
    }

    accessWidenerPath = project(":Common").file("src/main/resources/${mod_id}.accesswidener")
}


tasks.withType(ProcessResources).configureEach {
    from project(":Common").sourceSets.main.resources
    var replaceProperties = [
            mod_name: mod_name,
            resource_pack_format_number: resource_pack_format_number,
            data_pack_format_number: data_pack_format_number,
            mod_id: mod_id,
            version: project.version,
            mod_author: mod_author,
            fabric_loader_version: fabric_loader_version,
            fabric_version: fabric_version,
            minecraft_version: minecraft_version,
            cca_version: cca_version,
            sbl_version: sbl_version,
            geckolib_version: geckolib_version,
            sherdsapi_version: sherdsapi_version,
            forgeconfigapiport_version: forgeconfigapiport_version,
            trimmed_version: trimmed_version,
            reachentityattributes_version: reachentityattributes_version,
            moonlight_version: moonlight_version,
            playeranimator_version: playeranimator_version,
            lambdynamiclights_version: lambdynamiclights_version,
            tes_version: tes_version,
            jade_version: jade_version
    ]
    inputs.properties replaceProperties

    filesMatching(['fabric.mod.json','pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":Common").sourceSets.main.allSource)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}